modeltype ECORE "strict" uses "http://www.eclipse.org/emf/2002/Ecore";
	

transformation bug467600(in e : ECORE);

main() {

	// test template versions of Model::createEmptyModel() and Model::copy()
	//
	do {
		var newModel : ECORE := ECORE.createEmptyModel();
		assert fatal (newModel.oclIsTypeOf(ECORE));
		assert fatal (newModel.objects()->size() = 0);
	};
	
	do {
		var newModel : ECORE := e.createEmptyModel();
		assert fatal (newModel.oclIsTypeOf(ECORE));
		assert fatal (newModel.objects()->size() = 0);
	};
	
	do {
		var newModel : ECORE := e.copy();
		assert fatal (newModel.oclIsTypeOf(ECORE));
		assert fatal (newModel.objects()->size() = 11);
	};
	
	do {
		var newModel : Model := e;
		var copyModel : Model := newModel.copy();
		assert fatal (copyModel.oclIsTypeOf(ECORE));
		assert fatal (copyModel.objects()->size() = 11);
	};
	
	do {
		var newModel : Model := Model.createEmptyModel();
		assert fatal (newModel.oclIsTypeOf(Model));
		assert fatal (newModel.objects()->size() = 0);
	};


	var pack : EPackage := e.objectsOfType(EPackage)->any(true);
	

	// test templates version of Element::clone() and Element::deepclone()
	//
	do {
		var shallowCopy : EPackage := pack.clone();
		assert fatal (shallowCopy.oclIsTypeOf(EPackage));
		assert fatal (shallowCopy.oclIsKindOf(ENamedElement));
		assert fatal (shallowCopy.name = pack.name);

		var deepCopy : EPackage := pack.deepclone();
		assert fatal (deepCopy.oclIsTypeOf(EPackage));
		assert fatal (deepCopy.oclIsKindOf(ENamedElement));
		assert fatal (deepCopy.name = pack.name);
		assert fatal (deepCopy.allSubobjects()->size() = pack.allSubobjects()->size());
	};

	do {
		var shallowCopy : EModelElement := pack.clone();
		assert fatal (shallowCopy.oclIsTypeOf(EPackage));
		assert fatal (shallowCopy.oclIsKindOf(ENamedElement));

		var deepCopy : EModelElement := pack.deepclone();
		assert fatal (deepCopy.oclIsTypeOf(EPackage));
		assert fatal (deepCopy.oclIsKindOf(ENamedElement));
		assert fatal (deepCopy.allSubobjects()->size() = pack.allSubobjects()->size());
	};

	do {
		var genElem : Element := pack;

		var shallowCopy : Element := genElem.clone();
		assert fatal (shallowCopy.oclIsTypeOf(EPackage));
		assert fatal (shallowCopy.oclIsKindOf(ENamedElement));

		var deepCopy : Element := genElem.deepclone();
		assert fatal (deepCopy.oclIsTypeOf(EPackage));
		assert fatal (deepCopy.oclIsKindOf(ENamedElement));
		assert fatal (deepCopy.allSubobjects()->size() = pack.allSubobjects()->size());
	};


	// test various Model.objects*()
	//
	do {
		var v : Set(Element) := e.objects();
		assert fatal (v->size() = 11);
	};
	do {
		var v : Set(OclAny) := e.objects();
		assert fatal (v->size() = 11);
	};
	
	do {
		var v : Set(ECORE::EPackage) := e.objectsOfType(EPackage);
		assert fatal (v->size() = 1); 
	};
	
	do {
		var v : Set(EModelElement) := e.objectsOfKind(EClassifier);
		assert fatal (v->size() = 3);
	};
	
	
	// test various Element.subobjects*()
	//
	do {
		var v : Set(Stdlib::Element) := pack.subobjects();
		assert fatal (v->size() = 3);
	};

	do {
		var v : Set(oclstdlib::OclAny) := pack.allSubobjects();
		assert fatal (v->size() = 10);
	};

	do {
		var v : Set(ECORE::EClassifier) := pack.subobjectsOfKind(EClassifier);
		assert fatal (v->size() = 3);
	};

	do {
		var v : Set(EDataType) := pack.subobjectsOfType(EEnum);
		assert fatal (v->size() = 1);
	};

	do {
		var v : Set(EModelElement) := pack.allSubobjectsOfKind(ETypedElement);
		assert fatal (v->size() = 3);
	};

	do {
		var v : Set(ETypedElement) := pack.allSubobjectsOfType(EAttribute);
		assert fatal (v->size() = 2);
		
		var attr : ETypedElement := v->asSequence()->at(1);
		var attr2 : ETypedElement := v->asSequence()->at(2);

		assert fatal(pack.allSubobjectsOfType(EAttribute)->includes(attr));
		assert fatal(not pack.allSubobjectsOfType(EAttribute)->excludes(attr));
		assert fatal(pack.allSubobjectsOfType(EAttribute)->count(attr) = 1);

		assert fatal(pack.allSubobjectsOfType(EAttribute)->includesAll(Sequence{attr, attr2}));
		assert fatal(not pack.allSubobjectsOfType(EAttribute)->excludesAll(Sequence{attr, attr2}));

		assert fatal(pack.allSubobjectsOfType(EAttribute) = Set{attr, attr2});
		assert fatal(not (pack.allSubobjectsOfType(EAttribute) <> Set{attr, attr2}));
	};


	// test various allInstances()
	//
	do {
		var v : Set(EModelElement) := e.allInstances(ENamedElement);
		assert fatal (v->size() = 9);
	};
	
	do {
		var v : Set(EModelElement) := ENamedElement.allInstances();
		assert fatal (v->size() = 39);
	};
	
	do {	
		var v : Set(EModelElement) := pack.allInstances(ENamedElement);
		assert fatal (v->size() = 8);
	};


	// test various dump() and repr()
	//
	do {
		var o : Stdlib::Object := null; 
		o.dump();
		dump(o);
		log(o.repr());
	};
	
	do {	
		var e : Stdlib::Element := pack;
		e.dump();
		dump(e);
		log(e.repr());
	};
	
	do {	
		var u : ECORE::ENamedElement := pack;
		u.dump();
		dump(u);
		log(u.repr());
	};
	
	do {	
		var c : Collection(Element) := Sequence{pack, pack};
		c->dump();
		dump(c);
		log(c->repr());
	};	
	
	do {	
		var c : Collection(String) := Dict{1="a", 2="b"};
		c->dump();
		dump(c);
		log(c->repr());
	};	
	
	do {	
		var c : Dict(Integer, String) := Dict{1="a", 2="b"};
		c->dump();
		dump(c);
		log(c->repr());
	};	
	
	do {	
		var c : List(String) := List{"a", "b"};
		c->dump();
		dump(c);
		log(c->repr());
	};	
	
	do {	
		var c : Tuple(s:String) := Tuple{s="a"};
		c.dump();
		dump(c);
		log(c.repr());
	};	
	
	do {	
		invalid.dump();
		dump(null);
		dump(invalid);
		log(invalid.repr());
	};	
	
}
