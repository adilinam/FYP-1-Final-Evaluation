modeltype e uses ecore('http://www.eclipse.org/emf/2002/Ecore');

transformation bug428618();

main() {

	var cc1 : Collection(Integer);
	cc1 := Sequence{1, 1};
	assert fatal (cc1->size() = 2);
   
	var ccOrig : Sequence(String) := List{"a"};
	assert fatal (ccOrig = Sequence{"a"});
	
	var ccCopy : Sequence(String) := ccOrig;
	ccCopy += "b";
	assert fatal (ccOrig = Sequence{"a"});

	var c : Collection(String);
	assert fatal (c = null);
	
	c := Sequence{"a"};
	c += "b";
	assert fatal (c = Sequence{"a", "b"});
	assert fatal (c <> Set{"a", "b"});
	
	c := Set{"a"};
	c += "b";
	assert fatal (c = Set{"a", "b"});
	assert fatal (c <> Sequence{"a", "b"});

	var p : EPackage = object EPackage {};
	
	-- check deferred initialization
	var coll1 : Collection(EPackage);
	coll1 := OrderedSet{p};
	assert fatal (coll1->includes(p));
	
	-- check changed collection type
	coll1 := Sequence{p};
	coll1 += p;		
	assert fatal (coll1->size() = 2);
	
	-- check incremental assignment of 'invalid'
	coll1 += invalid;
	assert fatal (coll1->oclIsUndefined());
	
	-- check mutation of List
	coll1 := List{};
	var coll2 : Collection(EPackage) = coll1;
	coll2 += p;
	assert fatal (coll1->includes(p));
	
	coll1 := List{};
	coll2 := coll1;
	coll2 += p;
	assert fatal (coll1->includes(p));
	
	-- check implicit initialization on declaration
	var coll3 : OrderedSet(EPackage);
	assert fatal(coll3->isEmpty());
	
	-- check conversion to empty collection on explicit 'null' assignment
	coll3 += p;
	assert fatal (coll3->includes(p));
	coll3 := null;
	assert fatal (coll3->isEmpty());
	
	-- check that uninitialized abstract 'Collection' type is converted to Set{}
	var c1 : Collection(OclAny);
	c1 += "a";
	assert fatal (c1 = Set{"a"});
	
	-- check implicit result initialization inside mapping 'init' section
	map m1();
	
	-- check implicit initialization inside tuple result
	map m2();
		
}

mapping m1() : OrderedSet(EPackage) {
	
	init {
		assert fatal (result->isEmpty());
	}
	
}

mapping m2() : r1 : OrderedSet(EPackage), r2 : EPackage {
	
	init {
		assert fatal (r1->isEmpty());
		
		r1 += object EPackage{};
		
		assert fatal (r1->size() = 1);
	}
	
	assert fatal (not result->oclIsUndefined());
	
	assert fatal (not r2.oclIsUndefined());
	assert fatal (r1->size() = 1);
	
	assert fatal (r1 = result.r1);
	assert fatal (r2 = result.r2);
} 

